buildscript {
    ext {
        springBootVersion = '2.4.1'
        testcontainersVersion = '1.15.1'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'groovy'

    repositories {
        mavenCentral()
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.boot:spring-boot-dependencies:${springBootVersion}"
        }
    }

    dependencies {
        // spring boot
        implementation('org.springframework.boot:spring-boot-starter-webflux')
        annotationProcessor('org.springframework.boot:spring-boot-configuration-processor')


        //test
        testImplementation('io.projectreactor:reactor-test')
        implementation 'org.codehaus.groovy:groovy:3.0.7'
        testImplementation('org.spockframework:spock-core:2.0-M3-groovy-3.0')
        testImplementation('org.spockframework:spock-spring:2.0-M3-groovy-3.0')
        testImplementation('org.springframework.boot:spring-boot-starter-test') {
            exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
        }

        testRuntimeOnly "net.bytebuddy:byte-buddy:1.10.10"
        testRuntimeOnly "org.objenesis:objenesis:3.1"

        // lombok
        compileOnly('org.projectlombok:lombok')
        annotationProcessor('org.projectlombok:lombok')
        testCompileOnly('org.projectlombok:lombok')
        testAnnotationProcessor('org.projectlombok:lombok')
    }

    sourceCompatibility = '11'

    ext {
        projectName = "$project.name"
    }

    test {
        useJUnitPlatform()
        testLogging {
            events "passed", "skipped", "failed"
        }
    }
}

project(':trello-library') {

    dependencies {
    }
}

project(':discord-service') {

    apply plugin: 'org.springframework.boot'

    springBoot {
        mainClassName = 'pl.flyingoctopus.discord.DiscordServiceApplication'
    }

    dependencies {
        implementation project(':trello-library')

        // discord
        implementation('com.discord4j:discord4j-core:3.1.3')

        // database
        implementation('org.springframework.boot:spring-boot-starter-data-r2dbc')
        runtimeOnly('io.r2dbc:r2dbc-postgresql')
        runtimeOnly('org.postgresql:postgresql')
        runtimeOnly('com.h2database:h2')
        runtimeOnly('io.r2dbc:r2dbc-h2')
        implementation "org.flywaydb:flyway-core:7.4.0"


        // test
        testImplementation('org.testcontainers:postgresql')
        testImplementation('org.testcontainers:r2dbc')
        testImplementation('org.testcontainers:spock')
    }

    dependencyManagement {
        imports {
            mavenBom "org.testcontainers:testcontainers-bom:${testcontainersVersion}"
        }
    }
}
